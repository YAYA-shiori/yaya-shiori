///////// 文　ゴーストテンプレート
///////// マウス入力関連辞書
///////// written by umeici.

//**** 撫で反応 *************************************************************************

//---- OnMouseMoveイベント --------------------------------------------------------------
OnMouseMove
{
	//---- reference3を見て、本体側かkero側かを判断する
	if reference3 == 0
	{
		//---- 本体側の撫で
		MouseMoveSakura
	}
	else
	{
		//---- kero側の撫で
		MouseMoveKero
	}
}

//---- 本体側の撫で反応 -----------------------------------------------------------------
MouseMoveSakura
{
	if reference4 != ""
	{
		//----どこかが撫でられている
		stroke++
		//---- 撫でられた量が一定量に達したら「なでられている」と判断
		if stroke >= 64
		{
			//---- 撫でられた。撫でられた部位を見てトークする
			if reference4 == "Head"
			{
				"\0\s[0]えへへ\w5‥\w5‥\e"
			}
			elseif reference4 == "Bust"
			{
				"\0\s0やだ、どこをさわってるんですか。\w5\1\s[10]%username、あんたなあ\w5‥\w5‥。\e"
			}
			stroke = 0
		}
	}
	else
	{
		// 定義された部位はどこも撫でられていない
		stroke = 0
	}
}

//---- kero側の撫で反応 -----------------------------------------------------------------
MouseMoveKero
{
	//---- なし
}


//**** つつかれ反応 *********************************************************************

//---- OnMouseDoubleClickイベント -------------------------------------------------------
OnMouseDoubleClick
{
	//---- reference3を見て、本体側かkero側かを判断する
	if reference3 == 0
	{
		//---- 本体側のつつかれ
		MouseDoubleClickSakura
	}
	else
	{
		//---- kero側のつつかれ
		MouseDoubleClickKero
	}
}

//---- 本体側のつつかれ反応 -------------------------------------------------------------
MouseDoubleClickSakura
{
	//---- 顔つつかれ処理
	if reference4 == "Face"
	{
		//---- 顔つつかれ反応
		"\0\s[0]いたいいたいっ！\e"
	}
	else
	{
		//---- 顔以外のつつきでメニュー展開
		OpenMenu
	}
}

//---- kero側のつつかれ反応 -------------------------------------------------------------
MouseDoubleClickKero
{
	//---- kero側のつつかれ処理
	"\1\s[10]やめてくれ、ちょうどそこらへんにプロセッサがあるんだ。/
		\0\w6\s[0]アスロンだったっけ？\1\w6\n\nんなわけないだろ。Ｚ８０だ。\e"
	"\1\s[10]ああ、くすぐったいぞ。\e"
	"\1\s[10]ああん、もっとォ！\0キケンな冗談はやめなさい。\e"
}
