///////// YAYA　ゴーストテンプレート
///////// メニュー処理辞書

//**** メインメニューの展開 *************************************************************

//---- メインメニュー展開 ---------------------------------------------------------------
OpenMenu
{
	"\0\s[0]"
	--
	"はい、なんでしょうか？"
	"%username、なんですか？"
	"ん‥‥どうしたの？"
	--
	"\n\n"
	--
	AYATEMPLATE.MenuItem('なんか話して','MENU_AITALK') + '\n'
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem('さっきの話は？','MENU_LASTTALK') + '\n'
	}
	--
	AYATEMPLATE.MenuItem('しゃべり頻度を変えて','MENU_TALKINTERVAL') + '\n'
	--
	AYATEMPLATE.MenuItem('コミュニケート','MENU_COMMUNICATEOPEN') + '\n'
	--
	AYATEMPLATE.MenuItem('AIについて','MENU_ABOUTAYA') + '\n\n[half]'
	--
	AYATEMPLATE.MenuItem('なんでもない','MENU_CANCEL')
}


//**** メニュー項目選択 *****************************************************************

//---- メインメニュー
MENU_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
MENU_LASTTALK
{
	AYATEMPLATE.LastTalk
}

//---- トーク頻度の調整
MENU_TALKINTERVAL
{
	"\0\s[0]どのくらいがいいのかな？\n\n"
	--
	AYATEMPLATE.MenuItem('うるさいくらい','MENU_TALKMAX') + '\n'
	--
	AYATEMPLATE.MenuItem('ふつう','MENU_TALKNORMAL') + '\n
	--
	AYATEMPLATE.MenuItem('静か','MENU_TALKPOOR') + '\n'
	--
	AYATEMPLATE.MenuItem('黙ってて','MENU_TALKNONE') + '\n\n[half]'
	--
	AYATEMPLATE.MenuItem('戻る','OpenMenu')
}

//---- 文について説明
MENU_ABOUTAYA
{
	"\0\s[0]わたしたちは汎用DLL「文」で制作されています。/
		\1\w9C 言語風の読みやすいスクリプトで、SHIORIやSAORIを作ることができる。\w9/
		\0\n\n\s[0]んー。そんなとこですね。\e"
}

//---- コミュニケートボックスオープン
MENU_COMMUNICATEOPEN
{
	"\0\s[0]はいっ。\![open,communicatebox]\e"
}

//---- キャンセルされた
MENU_CANCEL
{
	"\0\s[0]‥\w6‥\w6むー。\e"
}

//---- トーク頻度
MENU_TALKMAX
{
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\0\s[0]よーし、話しちゃうぞーっ。\1げへへーっ。\e"
}

MENU_TALKNORMAL
{
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\0\s[0]ふつうにね。\1うん。\e"
}

MENU_TALKPOOR
{
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\0\s[0]控えめにするね。\1物足りないな。\e"
}

MENU_TALKNONE
{
	//---- 話さない
	aitalkinterval = 0
	"\0\s[0]‥\w6‥\w6そ、そんなあ‥\w6‥\w6。\1仕方ないだろ。\e"
}

//---- ヘッドライン
MENU_HEADLINECANCEL
{
	//---- キャンセルされた
	"\0\s[0]はい。\e"
}

//---- メニュー選択タイムアウト ---------------------------------------------------------
OnChoiceTimeout
{
	"\0\s[0]ちゃんと選んでくださいっ！\e"
}
