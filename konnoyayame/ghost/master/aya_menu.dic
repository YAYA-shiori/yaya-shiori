///////// 文　ゴーストテンプレート
///////// メニュー処理辞書
///////// written by umeici.

//**** メインメニューの展開 *************************************************************

//---- メインメニュー展開 ---------------------------------------------------------------
OpenMenu
{
	"\0\s[0]"
	--
	"はい、なんでしょうか？"
	"%username、なんですか？"
	"ん‥‥どうしたの？"
	--
	"\n\n/
	\q[なんか話して,AITALK]\n/
	\q[しゃべり頻度を変えて,TALKINTERVAL]\n/
	\q[コミュニケート,COMMUNICATEOPEN]\n/
	\q[AIについて,ABOUTAYA]\n/
	\q[なんでもない,CANCEL]\e"
}


//**** メニュー項目選択 *****************************************************************

//---- OnChoiceSelectイベント -----------------------------------------------------------
OnChoiceSelect
{
	//---- メインメニュー
	if reference0 == "AITALK"
	{
		//---- 明示的にAIトーク開始を指示
		OnAiTalk
	}
	elseif reference0 == "TALKINTERVAL"
	{
		//---- トーク頻度の調整
		"\0\s[0]どのくらいがいいのかな？\n\n/
		\q[うるさいくらい,TALKMAX]\n/
		\q[ふつう,TALKNORMAL]\n/
		\q[静か,TALKPOOR]\n/
		\q[黙ってて,TALKNONE]\e"
	}
	elseif reference0 == "ABOUTAYA"
	{
		//---- 文について説明
		"\0\s[0]わたしたちは汎用DLL「文」で制作されています。/
			\1\w9C 言語風の読みやすいスクリプトで、SHIORIやSAORIを作ることができる。\w9/
			\0\n\n\s[0]んー。そんなとこですね。\e"
	}
	elseif reference0 == "COMMUNICATEOPEN"
	{
		//---- コミュニケートボックスオープン
		"\0\s[0]はいっ。\![open,communicatebox]\e"
	}
	elseif reference0 == "CANCEL"
	{
		//---- キャンセルされた
		"\0\s[0]‥\w6‥\w6むー。\e"
	}
	//---- トーク頻度
	elseif reference0 == "TALKMAX"
	{
		//---- うるさく話す(15秒間隔)
		aitalkinterval = 15
		"\0\s[0]よーし、話しちゃうぞーっ。\1げへへーっ。\e"
	}
	elseif reference0 == "TALKNORMAL"
	{
		//---- ふつうに話す(3分間隔)
		aitalkinterval = 180
		"\0\s[0]ふつうにね。\1うん。\e"
	}
	elseif reference0 == "TALKPOOR"
	{
		//---- 静かに話す(5分間隔)
		aitalkinterval = 300
		"\0\s[0]控えめにするね。\1物足りないな。\e"
	}
	elseif reference0 == "TALKNONE"
	{
		//---- 話さない
		aitalkinterval = 0
		"\0\s[0]‥\w6‥\w6そ、そんなあ‥\w6‥\w6。\1仕方ないだろ。\e"
	}
	//---- ヘッドライン
	elseif reference0 == "HEADLINECANCEL"
	{
		//---- キャンセルされた
		"\0\s[0]はい。\e"
	}
}

//---- メニュー選択タイムアウト ---------------------------------------------------------
OnChoiceTimeout
{
	"\0\s[0]ちゃんと選んでくださいっ！\e"
}
