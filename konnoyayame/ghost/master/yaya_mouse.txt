//******************************************************************************
// YAYA　ゴーストテンプレート
// マウス入力関連辞書
//******************************************************************************


//******************************************************************************
//マウス処理部
//
//以下、実際のトークを記述します。関数名は以下の規則になっています。
//[種別] + [スコープ] + [部位]
//種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
//スコープは、\0,\1,といったスコープ番号です。
//部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
//部位はありません。
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************
MouseMove0Head
{
	"\0\s[0]えへへ\w5‥\w5‥\e"
	"\0\s[5]なでなでされると、気持ちが良いよね。\w8\1お子様だからな。\w8\0\s[3]\n\nぷー。\e"
	"\0\s[1]‥\w5‥\w5\w8\1照れてる照れてる。\w8\0\n\n何を企んでるの？\w8\1\s[11]\n\nなっ！？\e"
	"\0\s[3]さ、触り反応は、難しいんですよ？\w8\1説明するのか照れるのか、どっちかにしろよ。\e"
}

MouseMove0Bust
{
	"\0\s0やだ、どこをさわってるんですか。\w5\1\s[10]%username、あんたなあ\w5‥\w5‥。\e"
	"\0\s[0]えっちなのはいけないとおもうんです。\w8\1冷静に対処するんじゃないよ。\e"
	"\0\s[7]そういうことは、%(ghost)にしてください。\w8\1\s[11]うわ最低。\e"
	"\0\s[5]ロリコン。\w8\1‥\w5‥\w5さらっと酷い事を。\e"
}

//******************************************************************************
//つつかれ反応
//******************************************************************************

//本体側のつつかれ反応
MouseDoubleClick0Face
{
	"\0\s[0]いたいいたいっ！\e"
	"\0\s[7]ばかになったらどうするんですか！\w8\1違ったのか。\w8\0\s[3]\n\nぷー‥\w5‥\w5\e"
	"\0\s[3]うう‥\w5‥\w5いたいよぉ‥\w5‥\w5\w8\1挙動テストだと思えば、痛くない痛くない。\w8\0\n\nマック朗にやってくださいよ‥\w5‥\w5\w8\1\s[-1]\n\n\w8\0\s[2]\n\nああっ！逃げちゃダメだってば！\e"
}

MouseDoubleClick0Bust
{
	"\0\s[7]‥\w5‥\w5‥\w5\w8\1怒ってる怒ってる。\e"
	"\0\s[5]すけべ。\w8\1ゴーストの基本機能って話も‥\w5‥\w5\w8\0\s[6]\n\nないです。\e"
	"\0\s[1]‥\w5‥\w5はずかしいんですけど。\w8\1あまりにも平らだからか。\w8\0\s[5]\n\n‥\w5‥\w5%(username)、りんごジュース飲みますか？\w8\1\s[11]\n\nやめてあげてっ！！\e"
}

MouseDoubleClick0
{
	//顔以外のつつきでメニュー展開
	OpenMenu
}

//kero側のつつかれ反応
MouseDoubleClick1
{
	//kero側のつつかれ処理
	"\1\s[10]やめてくれ、ちょうどそこらへんにプロセッサがあるんだ。/
		\0\w6\s[0]アスロンだったっけ？\1\w6\n\nんなわけないだろ。Ｚ８０だ。\e"
	"\1\s[10]ああ、くすぐったいぞ。\e"
	"\1\s[10]ああん、もっとォ！\0キケンな冗談はやめなさい。\e"
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

OnMouseMove
{
	AYATEMPLATE.MouseMoveWheelFunc(0,50,'MouseMove')
}

OnMouseWheel
{
	_result = ''
	if TOINT(reference[2]) > 0 {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
	}
	else {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
	}
	
	if _result = '' {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
	}
	
	_result
}

OnMouseDoubleClick
{
	AYATEMPLATE.MouseEventExec('MouseDoubleClick');
}
