///////// YAYA　ゴーストテンプレート
///////// 汎用アプリケーション/どりふ/きのこ辞書

//**** 汎用アプリケーション *************************************************************

//---- 起動 -----------------------------------------------------------------------------
OnApplicationBoot
{
	"\0\s[0]%(reference[0])、起動っ。\e"
}

//---- 終了 -----------------------------------------------------------------------------
OnApplicationClose
{
	"\0\s[0]%(reference[0])、終了っ。\e"
}

//---- ファイルオープン -----------------------------------------------------------------
OnApplicationFileOpen
{
	"\0\s[0]%(reference[1])を開きました。\e"
}


//**** きのこ ***************************************************************************

//---- 生えた ---------------------------------------------------------------------------
OnKinokoObjectCreate
{
	"\1\s[10]おい、%(reference[0])が‥\w5‥\w5\0\s[0]‥\w5‥\e"
	"\0\s[0]重い。\e"
}

//---- 消えた ---------------------------------------------------------------------------
OnKinokoObjectDestroy
{
	"\0\s[0]ふー、やっと消えたか。\e"
}

//---- バッテリーが低下 -----------------------------------------------------------------
OnBatteryLow
{
	"\1\s[10]バッテリーが無くなってきてるようだぞ。\0\s[0]%username、気をつけて。\e"
}

//---- システムリソースが低下 -----------------------------------------------------------
OnSysResourceLow
{
	"\1\s[10]システムリソースが少なくなってきたようだ。\0\s[0]わ、わたしたちのせいかな？\1\n\n\w5どうだろうな。\e"
}


//**** どりふ ***************************************************************************

//---- OnNekodorifObjectHitイベント -----------------------------------------------------
OnNekodorifObjectHit
{
	if reference[4] == "ややめ"	// ここにsakura名を指定
	{
		ObjectHitSakura
	}
	else
	{
		ObjectHitKero
	}
}

//---- 本体側に当たった -----------------------------------------------------------------
ObjectHitSakura
{
	if reference[2][0] == "pain" || reference[2][0] == "stab"
	{
		"\0\s0いったあい！\e"
	}
	elseif reference[2][0] == "surprise" || reference[2][0] == "huge"
	{
		"\0\s0わっ！\e"
	}
	elseif reference[2][0] == "love" || reference[2][0] == "elegant" || /
		reference[2][0] == "pretty" || reference[2][0] == "food"
	{
		"\0\s0わーいっ。\e"
	}
	else
	{
		"\0\s0‥\w2‥\w2むー。%(reference[0])か。\e"
	}
}

//---- kero側に当たった -----------------------------------------------------------------
ObjectHitKero
{
	if reference[2][0] == "pain" || reference[2][0] == "stab"
	{
		"\1\s[10]ぐふっ‥\w2‥\w2！\e"
	}
	else
	{
		"\1\s[10]むぅ。\e"
	}
}
