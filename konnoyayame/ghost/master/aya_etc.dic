///////// YAYA　ゴーストテンプレート
///////// 種々のイベント辞書

//**** インストール関連 *****************************************************************

//---- インストール中のメッセージ -------------------------------------------------------
OnInstallBegin
{
	"\0\s[0]インストールしています‥‥\e"
}

//---- インストール完了メッセージ -------------------------------------------------------
OnInstallComplete
{
	"\0\s[0]"
	--
	if reference0 == "shell"
	{
		"シェル"
	}
	elseif reference0 == "ghost"
	{
		"ゴースト"
	}
	elseif reference0 == "balloon"
	{
		"バルーン"
	}
	elseif reference0 == "plugin"
	{
		"プラグイン"
	}
	--
	"「%reference1」、インストール完了しました。\e"
}

//---- インストール失敗メッセージ -------------------------------------------------------
OnInstallFailure
{
	"\0\s[0]インストールに失敗しましたー‥\w5‥\w5\n\n理由：%reference0。あーあ。\e"
}

//---- インストール拒否（他のゴーストのためのアーカイブだった） -------------------------
OnInstallRefuse
{
	"\1\s[10]これは、%reference0用のアーカイブだぞ。\w6\0\s[0]処理中断っ。\e"
}


//**** 消滅関連 *************************************************************************

//---- 消滅指示 -------------------------------------------------------------------------
OnVanishSelecting
{
	"\0\s[0]そ、そんな\w5‥\w5‥\1\s[10]あきらめるのはまだ早いぞ。\e"
}

//---- 消滅開始 -------------------------------------------------------------------------
OnVanishSelected
{
	"\0\s[0]そうですか。わかりました。\w5/
		\1\s[10]いままで付き合ってくれて、ありがとうな。\w5\0\s[0]\n\nさようなら。\e"
}

//---- 消滅中止 -------------------------------------------------------------------------
OnVanishCancel
{
	"\1\s[10]どうやら助かったな。\w5/
		\0\s[0]押し間違いかな？もっと細心の注意を払って、命懸けでマウスを操作してください。\e"
}

//---- 他のゴーストが消滅して自分に切り替わった -----------------------------------------
OnVanished
{
	"\0\s[0]%reference0と呼ばれていた物体よ。\w5\1\s[10]いつの日かあなたは復活するでしょう。\e"
	"\0\s[0]どうもー。\1\s[10]「こんなのも要らねえなこれも消滅しちまえ」とか思わないように。\0\w5\n\n泣くよ。\e"
}


//**** メールチェック *******************************************************************

//---- メールチェック開始 ---------------------------------------------------------------
OnBIFFBegin
{
	"\0\s[0]メールが届いてないかチェックしますね。\n\nマック朗、よろしく。\1\s[10]んむ。\e"
}

//---- メールチェック成功 ---------------------------------------------------------------
OnBIFFComplete
{
	if reference0 == 0
	{
		//---- メールは無かった
		"\1\s[10]メールは無かったな。\w5\0\s[0]ご苦労さん。\n\nだそうです%username。寂しい？\e"
	}
	else
	{
		//---- メールが届いていた
		"\1\s[10]%reference0通のメールがあったぞ。\w5\0\s[0]へー。%username、さっそく見てみなよ。\e"
	}
}

//---- メールチェック失敗 ---------------------------------------------------------------
OnBIFFFailure
{
	if reference0 == "timeout"
	{
		//---- タイムアウト
		"\1\s[10]いつまで待ってもメールサーバにつながらないんだが。\w5\0\s[0]困ったもんだ。\e"
	}
	elseif reference0 == "kick"
	{
		//---- 蹴られた
		"\1\s[10]蹴られたぞ。\0\s[0]おかしいですねー、なぜ‥\w5‥\e"
	}
	elseif reference0 == "defect"
	{
		//---- 設定誤り
		"\1\s[10]なんか、ぜんぜんダメなんだが。\w5\0\s[0]メールチェックの設定を見なおしてね、%username。\e"
	}
	else
	{
		//---- 原因不明
		"\1\s[10]原因はわからないが、失敗。\w5\0\s[0]ありゃー。\e"
	}
}


//**** ネットワーク更新 *****************************************************************

//---- ネットワーク更新開始 -------------------------------------------------------------
OnUpdateBegin
{
	"\0\s[0]ネットワークを介して頭がぐんぐん良くなるっ開始っ！\w5\1\s[10]ならねえよ。\e"
}

//---- ネットワーク更新準備完了 ---------------------------------------------------------
OnUpdateReady
{
	"\0\s[0]%reference0個のファイルをダウンロードします。\w5\1\s[10]ＯＨ！\e"
}

//---- ネットワーク更新終了 -------------------------------------------------------------
OnUpdateComplete
{
	if reference0 == "none"
	{
		//---- 更新すべきファイルが無かった
		"\1\s[10]あいや、盛り上がってるとこ水を差すようだが、対象ファイルが無いぞ。\w5\0\s[0]ぬあー。\e"
	}
	else
	{
		//---- 実際に更新が完了した
		"\0\s[0]更新完了〜。\w5\1\s[10]おつかれさん。\e"
	}
}

//---- ネットワーク更新失敗 -------------------------------------------------------------
OnUpdateFailure
{
	if reference0 == "timeout"
	{
		//---- タイムアウトした
		"\0\s[0]時間切れだ‥‥\w5\1\s[10]なんだろうな。"
	}
	elseif reference0 == "md5 miss"
	{
		//---- MD5値が一致しなかった
		"\0\s[0]MD5値が違ってましたぜ。\w5\1\s[10]ああー。"
	}
	else
	{
		//---- 上記以外の理由で失敗した
		"\0\s[0]%reference0、だって。\w5\1\s[10]うーん。"
	}
	--
	"\n\n\1ネットワーク更新、失敗だ。\e"
}

//---- ネットワーク更新 ダウンロード開始 ------------------------------------------------
OnUpdate.OnDownloadBegin
{
	"\0\s[0]ファイル「%reference0」をダウンロードします。\e"
}

//---- ネットワーク更新 MD5値照合開始 ---------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	"\0\s[0]MD5値のチェックを開始します。\e"
}

//---- ネットワーク更新 MD5値一致 -------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"\0\s[0]ん、オッケー。\e"
	"\0\s[0]いいみたいだね。\1\s[10]うむ。\w"
}

//---- ネットワーク更新 MD5値不一致 -----------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"\0\s[0]MD5不一致！\1\s[10]悲しい！\e"
}

//**** ヘッドラインセンス ***************************************************************

//---- ヘッドラインセンス開始 -----------------------------------------------------------
OnHeadlinesenseBegin
{
	"\0\s[0]%reference0ヘッドライン取得中。\e"
}

//---- ヘッドライン読み上げ -------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- はじめのセリフ
	if reference2 == "First" || reference2 == "First and Last"
	{
		//---- 最初のページ
		"\0\b2\s[0]%reference0のヘッドラインですよっ。"
	}
	elseif reference2 == "Next"
	{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]%reference0ヘッドライン、続き。"
	}
	elseif reference2 == "Last"
	{
		//---- 最後のページ
		"\0\b2\s[0]%reference0ヘッドライン、これが最後のページです。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%reference3\n\n"
	--
	//---- 選択肢
	if reference2 == "First" || reference2 == "Next"
	{
		//---- 次のページがある場合
		"\q0[][次のページ]/
		\q1[%reference1][サイトへ]/
		\q2[HEADLINECANCEL][やめ]\e"
	}
	else
	{
		//---- 次のページがない場合
		"\q0[%reference1][サイトへ]/
		\q1[HEADLINECANCEL][やめ]\e"
	}
}

//---- ヘッドライン更新が無かった -------------------------------------------------------
OnHeadlinesenseComplete
{
	"\0\s[0]%reference0、更新はないみたいです。\e"
}

//---- ヘッドラインセンス失敗 -----------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[0]ヘッドラインセンスに失敗しました。\n\n理由："
	--
	if reference0 == "can't download"
	{
		"ダウンロード失敗。"
	}
	elseif reference0 == "can't analyze"
	{
		"ヘッドライン解析失敗。"
	}
	else
	{
		"謎。"
	}
	--
	"\e"
}


//**** ファイル作成関連 *****************************************************************

//---- updates2.dau作成開始 -------------------------------------------------------------
OnUpdatedataCreating
{
	"\0\s[0]アップデートデータファイル作成中です。\e"
}

//---- updates2.dau作成完了 -------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[0]アップデートデータファイルの作成が終わりました。\e"
}

//---- NAR作成開始 ----------------------------------------------------------------------
OnNarCreating
{
	"\0\s[0]なー。作成しています。\e"
}

//---- NAR作成完了 ----------------------------------------------------------------------
OnNarCreated
{
	"\0\s[0]「%reference0」のなーファイル「%reference1」、できました。\e"
}

//****　時計合わせ　*********************************************************************
//**** みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏

//----　時計合わせ開始 ------------------------------------------------------------------
OnSNTPBegin
{
	"\0\s[0]時計あわせ開始、%reference0 に接続中。\e"
}

//----　時刻取得成功、比較開始 ----------------------------------------------------------
OnSNTPCompare
{
	"\0\s[0]/
	current %reference1[0]/%reference1[1]/%reference1[2] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	local   %reference2[0]/%reference2[1]/%reference2[2] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
	\n"
	--
	if reference3 == 0
	{
		"うーん、ぴったり。\e"
	}
	else
	{
		"\0\n\s[0]%reference3秒ずれています。\n\n/
		\q0[OnSNTPEXECUTE][時刻を合わせる]/
		\q1[OnSNTPCANCEL][放置]\e"
	}
}

//----　時計合わせ実行 ------------------------------------------------------------------
OnSNTPEXECUTE
{
	"\0\s[0]時刻を合わせました。\6\e"
}

//----　時計合わせ中止 ------------------------------------------------------------------
OnSNTPCANCEL
{
	"\0\s[0]中止。\e"
}

//----　時計合わせ失敗 ------------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[0]時計あわせ失敗。\e"
}


//**** そのほか *************************************************************************

//---- ディスプレイ解像度変更 -----------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference0
	displaywidth  = reference1
	displayheight = reference2
}

//---- URLドロップ ----------------------------------------------------------------------
//---- CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます ----
OnURLDropping
{
	"\0\s[0]%reference0からデータを取得中です。\e"
}

//---- 起動時に MATERIA を検出 ----------------------------------------------------------
OnEmbryoExist
{
	"\0\s[0]既に「伺か」が起動してますねー。\e"
}

//---- 起動時に猫どりふを検出 -----------------------------------------------------------
OnNekodorifExist
{
	"\0\s[0]うわ、猫どりふが‥‥\w9\1\s[10]‥\w5‥\5！！\e"
}

//---- スクリーンセーバー実行開始 -------------------------------------------------------
OnScreenSaverStart
{
	"\0\s[0]スクリーンセーバーが動き出しました\e"
}

//---- スクリーンセーバー実行終了 -------------------------------------------------------
OnScreenSaverEnd
{
	"\0\s[0]%username、お帰りなさい。\e"
}
