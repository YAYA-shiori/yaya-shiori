///////// YAYA　ゴーストテンプレート
///////// メニュー処理辞書

//**** 注意 *************************************************************
// この辞書はゴーストテンプレートに必要な共通処理をまとめたものです。
// 通常はここをいじる必要はありません。
//**** 注意 *************************************************************

//**** ランダムトーク *******************************************************************

OnAITalk
{
	OnAiTalk
}

//---- OnAiTalkイベント -----------------------------------------------------------------
OnAiTalk
{
	if mikireflag == 0
	{
		//---- 通常のランダムトーク、ただしチェイン中はチェイントーク
		if CHAIN.IDName == "" {
			RandomTalk
		}
		else {
			ChainTalk
		}
	}
	else
	{
		//---- 見切れ中なので見切れ中のトークを行う
		MikireTalk
	}
}

//**** メニュー関係 *************************************************************

//---- イベント処理 ---------------------------------------------------------------
//ここの記述により、選択肢ID/アンカーIDをそのまま関数名として書けるようになります。
//以下のサンプルも参照して下さい。

OnChoiceSelect
{
	if ISFUNC(reference[0]) {
		EVAL(reference[0])
	}
}

OnAnchorSelect
{
	if ISFUNC(reference[0]) {
		EVAL(reference[0])
	}
}

//---- メニュー横1行反転 ---------------------------------------------------------------
//通常メニュー
AYATEMPLATE.MenuItem
{
	'\![*]\q['
	--
	AYATEMPLATE.EscapeMenuText( AYATEMPLATE.MakeJustText(_argv[0],46) )
	--
	_n = ARRAYSIZE(_argv)
	_s = ''
	for _i = 1 ; _i < _n ; _i++ {
		_s += ',' + AYATEMPLATE.EscapeMenuText(_argv[_i])
	}
	_s
	--
	']'
}

//エスケープ
AYATEMPLATE.EscapeMenuText
{
	_r = _argv[0]
	if RE_SEARCH(_r,'["\[\]]') >= 0 {
		'"' + REPLACE(_r,'"','""') + '"'
	}
	else {
		_r
	}
}

//---- 後ろにスペースを補完する関数
AYATEMPLATE.MakeLongText
{
	_menuitem = _argv[0]
	_len = _argv[1] - GETSTRBYTES(_menuitem,127)

	if _len <= 0 {
		_menuitem
		return
	}
	
	_space = '　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　'
	_menuitem += SUBSTR(_space,0,_len / 2)
	
	if _len % 2 > 0 {
		_menuitem += " "
	}
	_menuitem
}

//---- 適当な長さで切る -------------------------------------------
AYATEMPLATE.MakeShortText
{
	if _argc < 2 {
		'';
		return;
	}
	
	_num = _argv[1] / 2
	_len = STRLEN(_argv[0])
	_text = ''
	
	if _num >= _len {
		_argv[0]
		return;
	}
	
	_lendiff = 0
	while 1 {
		_text = SUBSTR(_argv[0],0,_num)
		
		_lendiff = GETSTRBYTES(_text,127) - _argv[1] + 1
		if _lendiff >= 0 {
			break;
		}
		_num += 1
		if _num >= _len {
			break;
		}
	}
	
	if _num >= _len {
		_argv[0]
	}
	else {
		_text = SUBSTR(_argv[0],0,_num - 1)
		if _lendiff > 0 {
			_text += '..'
		}
		else {
			_text += '...'
		}
		_text;
	}
}

//---- 長さをきっちり調節
AYATEMPLATE.MakeJustText
{
	_text = AYATEMPLATE.MakeLongText(_argv[0],_argv[1])
	if _text != _argv[0] {
		_text
		return
	}
	
	_text = AYATEMPLATE.MakeShortText(_argv[0],_argv[1])
	_text
}

//**** リンクメニュー *************************************************************

AYATEMPLATE.LinkMenuConvert
{
    _text = ''
    _data = IARRAY
    for _i = 0 ; _i < _argc ; _i++ {
        _data = (RE_SPLIT(_argv[_i],'[ \t]*\|[ \t]*'),'','') //ダミー
        _text += "%(_data[0])%(CHR(1))%(_data[1])%(CHR(1))%(_data[2])%(CHR(1))%(CHR(2))"
    }
    _text;
}

//**** システムで使われる文字置換 *************************************************************

AYATEMPLATE.TranslateSystemChar
{
  _text = TOSTR(_argv[0])
  _text = RE_REPLACE(_text,"[ !%(CHR(0x22))%(CHR(0x25))#$&()*+,\-/:;<=>?@\[\]`{|}~]","_")
  _text
}

//**** 切り替えコア *************************************************************

AYATEMPLATE.ExecuteChangeCallTalk
{
	_ghostname = AYATEMPLATE.TranslateSystemChar(reference0)

	_funcname = "%(_argv[0])_%(_ghostname)"
	if ISFUNC(_funcname) {
		_script = EVAL(_funcname)
		if STRLEN(_script) != 0 {
			_script
			return
		}
	}

	_funcname = "%(_argv[0])Other"
		if ISFUNC(_funcname) {
		_script = EVAL(_funcname)
		if STRLEN(_script) != 0 {
			_script
			return
		}
	}

	if _argc >= 2 {
		_script = EVAL(_argv[1])
		if STRLEN(_script) != 0 {
			REPLACE(_script,"\-","")
			return
		}
	}
}

//**** マウスイベントコア *************************************************************

AYATEMPLATE.MouseEventExec
{
	_fname = _argv[0] + TOSTR(reference3) + TOSTR(reference4)
	if ISFUNC(_fname) {
		EVAL(_fname)
		return
	}
	
	_fname = _argv[0] + TOSTR(reference3)
	if ISFUNC(_fname) {
		EVAL(_fname)
		return
	}
}

AYATEMPLATE.MouseMoveWheelFunc
{
	_side = TOINT(reference3)
	if _argv[0] {
		_side += 10
	}
	
	if strokeid[_side] == reference4 {
		if (GETTICKCOUNT - TOINT(stroketimer[_side])) > 1500 {
			stroke[_side] = 1
		}
		stroketimer[_side] = GETTICKCOUNT
		
		_stroke = TOINT(stroke[_side])
		_stroke += 1
		stroke[_side] = _stroke
		
		if _stroke >= _argv[1] {
			AYATEMPLATE.MouseEventExec(_argv[2]);
			stroke[_side] = 1
		}
	}
	else {
		stroke[_side] = 1
		stroketimer[_side] = GETTICKCOUNT
		strokeid[_side] = reference4
	}
}

//**** 文字列関係 *************************************************************

//---- SHIORI 関連情報 ------------------------------------------------------------------
On_version
{
	GETSETTING('coreinfo.version')
}

On_craftman
{
	GETSETTING('coreinfo.author')
}

On_craftmanw
{
	GETSETTING('coreinfo.author')
}

On_name
{
	GETSETTING('coreinfo.name')
}


