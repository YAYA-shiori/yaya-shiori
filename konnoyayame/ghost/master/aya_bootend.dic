///////// YAYA　ゴーストテンプレート
///////// 起動/終了/切り替えトーク辞書

//**** 初回起動 *************************************************************************

//---- OnFirstBootイベント --------------------------------------------------------------
OnFirstBoot
{
	//---- 変数の初期化
	username         = "ユーザーさん"
	stroke           = 0	// 撫で判定用
	teachusername    = 0	// ユーザー名覚え用
	mikireflag       = 0	// 見切れ処理用

	//---- Vanishカウントを見て、真の初回か否かを判定する
	if reference0 == 0
	{
		//---- ほんとうに初めての起動
		"\0\s[0]\1\s[10]\0はじめまして。\w4わたしはややめ。\n\w4となりのやたらと白いのはホワ伊東。\w5/
		\1ほわいとです。製造単価二千円くらいです。\w5/
		\0\n\n腕なんか、もんじゃのヘラだしね。\w5/
		\1\n\nむ‥\w5‥\w5これと同じ会話を、最近どこかでだれかと交わしたような\w5‥\w5‥\w5‥？\w5/
		\0\n\nへえ、誰と？\w5/
		\1\n\n分からんが、お前とよく似た娘だったな。\x\c/
		\1まあそんなことはどうでもよろしい。\w5/
		\0よろしく、%username。\e"
	}
	else
	{
		//---- 再インストール後の初回起動
		"\0\s[0]また会いましたね。\w5/
		\1\s[10]前回は勘違いで消したのか？\w5"
		--
		if reference0 == 1
		{
			"\0\s[0]\n\nきっとそうだよ。\w4ね、%username。\w5"
		}
		else
		{
			"\0\s[0]\n\n‥\w5‥\w5%reference0回も消しておいて、いまさら勘違いだなんてことはないでしょ。\n/
			%username、飽きっぽいって言われない？\w5"
		}
		--
		"\1\n\n‥\w5‥\w5ま、\w9まあ、いいだろ。\0\n\nそうだね。またよろしく。\e"
	}
}


//**** 通常の起動 ***********************************************************************

//---- OnBootイベント -------------------------------------------------------------------
OnBoot
{
	//---- まず双方のサーフィスを表示
	"\0\s[0]\1\s[10]"
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に起動
		"\0おはよう、%usernameっ。\w5\1今日も一日がんばるぞ。\w5\0\n\n何を？\w5\1\n\n%msの尻を撫でる\w5‥\w5‥かな。\e"
		"\1おっす。\w5\0んー、わたし朝に弱くて。%usernameはどう？\e"
	}
	elseif _timeslot == "昼"
	{
		//---- 昼に起動
		"\0こんにちは、%username。お昼はもう食べたのかな？\w5\1なあ、俺らも%foodでも食おうよ。\w5"
		--
		"\0\n\n別のにしようよ\w5‥\w5‥\e"
		"\0\n\nそうだね、行こうか。\e"
	}
	elseif _timeslot == "日中"
	{
		//---- 日中に起動
		"\0こんにちは、%username。\w6\n\nお仕事は順調？勉強ははかどってるかな？\e"
	}
	elseif _timeslot == "夜"
	{
		//---- 夜に起動
		"\1夜だな。\w6\0ふふ、ホワ伊東は夜が好きだもんね。\w5\1\n\n当然だ。睡眠など時間の無駄。愚かなことだ。/
			\w5\0\n\nんー、あんたはロボットだからそれでもいいけどねー。\e"
		"\0こんばんは、%username。\w6\1なあ、%mhって何時閉店だっけ？\w5\0\n\nもう無理だよ。/
			\1\n\nふわああ、%WW2officerサイン入り%goods買いそびれたあ。\0\w5\n\n‥\w5‥\e"
	}
	else
	{
		//---- 深夜に起動
		"\0まーよーなーかーだー。\w5\1\s[10]腹減った。%food食いてえにょ。\w5\0\n\n太るよ\w5‥\w5‥。\e"
		"\1%hour12爺%minute糞だす。\w5\0‥\w5‥\w5ホワ伊東、もう寝ろ。\e"
	}
}

//---- 朝/昼/夜などをおおざっぱに調べる関数 -------------------------------------------
GetTimeSlot
{
	if hour >= 4 && hour <= 11
	{
		"朝"
	}
	elseif hour == 12 || hour == 13
	{
		"昼"
	}
	elseif hour >= 14 && hour <= 17
	{
		"日中"
	}
	elseif hour >= 18 && hour <= 21
	{
		"夜"
	}
	else
	{
		"深夜"
	}
}


//**** 終了 *****************************************************************************

//---- OnCloseイベント ------------------------------------------------------------------
OnClose
{
	"\1\s[10]ん、終わりか。\w5\0\s[0]"
	--
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に終了
		"徹夜明けかな？\w6\nおやすみ。"
	}
	elseif _timeslot == "昼" || _timeslot == "日中"
	{
		//---- 昼/日中に終了
		"じゃ、また。"
		"さよならっ。"
	}
	else
	{
		//---- 夜/深夜に終了
		"おやすみなさーい。"
	}
	--
	"\-\e"
}


//**** 最小化からの復帰 *****************************************************************

//---- OnWindowStateRestoreイベント -----------------------------------------------------
OnWindowStateRestore
{
	"\1\s[10]やれやれ。\0\s[0]\e"
	"\0\s[0]ふー、復帰だよ。\1\s[10]うむ。\e"
}


//**** ゴースト/シェル切り替え **********************************************************

//---- 他のゴーストへ切り替え -----------------------------------------------------------
OnGhostChanging
{
	if reference1 == "manual"
	{
		//---- 手動切り替え
		"\0\s[0]%reference0によろしくねー。\w5\1またな。\e"
	}
	else
	{
		//---- 自動切換え
		"\0\s[0]えーと、%reference0がしゃしゃり出たいと言ってるようなので交代しますね。\w5/
			\1おまえ‥\w5‥\w5そういう口を利くのは止せよ。\e"
	}
}

//---- 他のゴーストから切り替り ---------------------------------------------------------
OnGhostChanged
{
	"\0\s[0]あ、%username、どうも。\w5\1\s[10]うわさでは、%reference0って"
	--
	"%WW2officerの真の姿"
	"こないだまで%mhでバイトしてた"
	"クローン人間"
	"%meをたっぷり積んだ%vehicleに乗って%mpを徘徊してた"
	--
	"らしいぞ。\0\w5\n\nへ、へえ‥\w5‥\w5‥"
	--
	"地獄絵図だ"
	"ほのぼのしてる"
	"それもひどい"
	--
	"ね‥\w5‥\w5‥\e"
}

//---- シェルチェンジ開始 ---------------------------------------------------------------
OnShellChanging
{
	"\0\s[0]シェル変更開始。\e"
}

//---- シェルチェンジ完了 ---------------------------------------------------------------
OnShellChanged
{
	"\0\s[0]%reference0シェルに替わりました。どうですか？\w6\1\s[10]イイ！\e"
}



