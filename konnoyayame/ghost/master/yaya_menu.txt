//******************************************************************************
// YAYA　ゴーストテンプレート
// メニュー処理辞書
//******************************************************************************

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OpenMenu
{
	"\0\s[5]はい、なんでしょうか？"
	"\0\s[5]%username、なんですか？"
	"\0\s[0]ん‥‥どうしたの？"
	--
	AYATEMPLATE.MenuItem("なんか話して","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("さっきの話は？","Menu_LASTTALK")
	}
	--
	AYATEMPLATE.MenuItem("しゃべり頻度を変えて","Menu_TALKINTERVAL")
	--
	AYATEMPLATE.MenuItem("他のゴーストと話す","Menu_GHOSTCOMSTART")
	--
	AYATEMPLATE.MenuItem("ややめに話しかける","Menu_COMMUNICATEOPEN")
	--
	//スタンプ帳チェック
	if AYATEMPLATE.IsPluginExist('379E0DA0-131C-11DD-E1AC-90009E101CFE') {
		AYATEMPLATE.MenuItem("スタンプを押す","Menu_STAMP")
	}
	--
	AYATEMPLATE.MenuItem("AIについて","Menu_ABOUTAYA")
	--
	AYATEMPLATE.MenuItem("なんでもない","Menu_CANCEL",150)
}

//------------------------------------------------------------------------------
//メニュー選択タイムアウト
//------------------------------------------------------------------------------
OnChoiceTimeout
{
	"\0\s[7]ちゃんと選んでくださいっ！\e"
}


//******************************************************************************
//各項目の処理内容
//******************************************************************************

//---- 何か話して
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}

//---- トーク頻度の調整
Menu_TALKINTERVAL
{
	"\0\s[0]どのくらいがいいのかな？"
	--
	AYATEMPLATE.MenuItem("うるさいくらい","Menu_TALKMAX",200)
	--
	AYATEMPLATE.MenuItem("ふつう","Menu_TALKNORMAL")
	--
	AYATEMPLATE.MenuItem("静か","Menu_TALKPOOR")
	--
	AYATEMPLATE.MenuItem("黙ってて","Menu_TALKNONE")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

//---- 文について説明
Menu_ABOUTAYA
{
	"\0\s[0]わたしたちは汎用DLL「文」で制作されています。/
		\1\w9C 言語風の読みやすいスクリプトで、SHIORIやSAORIを作ることができる。\w9/
		\0\n\n\s[0]んー。そんなとこですね。\e"
}

//---- ややめに話しかける
Menu_COMMUNICATEOPEN
{
	"\0\s[0]はいっ。\![open,communicatebox]\e"
}

//---- 他のゴーストと話す
Menu_GHOSTCOMSTART
{
	StartCommunicate
}

//---- キャンセルされた
Menu_CANCEL
{
	"\0\s[0]‥\w6‥\w6むー。\e"
}

//---- トーク頻度
Menu_TALKMAX
{
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\0\s[0]よーし、話しちゃうぞーっ。\1げへへーっ。\e"
}

Menu_TALKNORMAL
{
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\0\s[0]ふつうにね。\1うん。\e"
}

Menu_TALKPOOR
{
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\0\s[0]控えめにするね。\1物足りないな。\e"
}

Menu_TALKNONE
{
	//---- 話さない
	aitalkinterval = 0
	"\0\s[0]‥\w6‥\w6そ、そんなあ‥\w6‥\w6。\1仕方ないだろ。\e"
}

//---- ヘッドライン
Menu_HEADLINECANCEL
{
	//---- キャンセルされた
	"\0\s[0]はい。\e"
}

//---- スタンプ帳
Menu_STAMP
{
	//初期化
	SHIORI3FW.RegisterTempVar('StampInfo_IsExist','StampInfo_IsNotify','StampInfo_CurrentStampFile','StampInfo_CurrentStampTitle')
	
	StampInfo_IsExist = 0
	StampInfo_IsNotify = 0
	
	//スタンプ帳情報を取った後、raiseでOnStampMenuExecを発生させて下に処理を移す
	"\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampInfoCall]\![raise,OnStampMenuExec]"
}

OnStampInfo
{
	//スタンプあるかどうか？
	StampInfo_IsExist = reference[1]
	StampInfo_CurrentStampFile = ''
	StampInfo_CurrentStampTitle = ''
	
	//押している場合は、どのスタンプを押しているか？
	_stamparray = SPLIT(_reference[3],CHR(2))
	
	_n = ARRAYSIZE(_stamparray)
	for _i = 0 ; _i < _n ; _i++ {
		_stamp = RE_SPLIT(_stamparray[_i],'[\x1,]') //バイト値1かカンマかでぶった切る：互換モード対策
		
		//ほかで使う場合はここの名前を変えてね
		if _stamp[0] == '紺野ややめ' {
			StampInfo_CurrentStampFile = _stamp[1]
			StampInfo_CurrentStampTitle = _stamp[2]
			break
		}
	}
}

OnStampMenuExec
{
	_stamp_type = ''
	
	//あった
	if StampInfo_IsExist {
	}
	//ない
	else {
		"\0\s[0]スタンプ？\w9\w9\n\s[5]はいどうぞ〜。\w9\w9\1\s[11]せっかくだから俺はこのスタンプを選ぶぜ！\w9\w9\0\s[4]えーっと。"
		
		_stamp_type = 'ややめ'
	}
	--
	if _stamp_type == 'ややめ' {
		//2行目ファイル、3行目タイトル、4行目コメント
		"\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stmp_yym.png,/
		ややめスタンプ,/
		紺野ややめが押したスタンプ。服のジッパーの取っ手とデザインが同じ。]"
	}
	else {
		"\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stmp_mcr.png,/
		マック郎スタンプ,/
		マック郎が押したスタンプ。黒林檎。見た目に反して毒はない…と信じたい。]"
	}
}
