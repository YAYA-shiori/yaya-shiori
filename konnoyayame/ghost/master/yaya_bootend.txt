//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- 変数の初期化
	username         = "ユーザーさん"
	teachusername    = 0	// ユーザー名覚え用

	//---- Vanishカウントを見て、真の初回か否かを判定する
	if reference[0] == 0
	{
		//---- ほんとうに初めての起動
		"\0\s[0]\1\s[10]\0はじめまして。\w4わたしはややめ。\n\w4となりのやたらと黒いのはマック朗。\w5/
		\1まっくろーです。なんでかかじられとるね。\w5/
		\0よろしく、%username。\e"
	}
	else
	{
		//---- 再インストール後の初回起動
		"\0\s[0]また会いましたね。\w5/
		\1\s[10]前回は勘違いで消したのか？\w5"
		--
		if reference[0] == 1
		{
			//vanish１回なら誤射かもしれない
			"\0\s[0]\n\nきっとそうだよ。\w4ね、%username。\w5"
		}
		else
		{
			"\0\s[0]\n\n‥\w5‥\w5%(reference[0])回も消しておいて、いまさら勘違いだなんてことはないでしょ。\n/
			%username、飽きっぽいって言われない？\w5"
		}
		--
		"\1\n\n‥\w5‥\w5ま、\w9まあ、いいだろ。\0\n\nそうだね。またよろしく。\e"
	}
}


//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************

OnBoot
{
	//---- まず双方のサーフィスを表示
	"\0\s[0]\1\s[10]"
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot
	
	case _timeslot{
	when "朝"
	{
		"\0おはよう、%usernameっ。\w5\1今日も一日がんばるぞ。\w5\0\n\n何を？\w5\1\n\n%msの尻を撫でる\w5‥\w5‥かな。\e"
		"\1おっす。\w5\0んー、わたし朝に弱くて。%usernameはどう？\e"
	}
	when "昼"
	{
		"\0こんにちは、%username。お昼はもう食べたのかな？\w5\1なあ、俺らも%foodでも食おうよ。\w5"
		--
		"\0\n\n別のにしようよ\w5‥\w5‥\e"
		"\0\n\nそうだね、行こうか。\e"
	}
	when "日中"
	{
		"\0こんにちは、%username。\w6\n\nお仕事は順調？勉強ははかどってるかな？\e"
	}
	when "夜"
	{
		"\0こんばんは、%username。\w6\1なあ、%mhって何時閉店だっけ？\w5\0\n\nもう無理だよ。/
			\1\n\nふわああ、%WW2officerサイン入り%goods買いそびれたあ。\0\w5\n\n‥\w5‥\e"
	}
	others
	{
		//それ以外の時間の場合
		"\0まーよーなーかーだー。\w5\1\s[10]腹減った。%food食いてえにょ。\w5\0\n\n太るよ\w5‥\w5‥。\e"
	}
	}//caseの終わり
}

//------------------------------------------------------------------------------
//朝/昼/夜などをおおざっぱに調べる関数
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
	when 22,23,0,1,2,3 {'深夜'}
	when 4,5,6,7,8,9,10,11 {'朝'}
	when 12,13 {'昼'}
	when 14,15,16,17 {'日中'}
	when 18,19,20,21 {'夜'}
	}//caseの終わり
}


//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
	"\1\s[10]ん、終わりか。\w5\0\s[0]"
	--
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に終了
		"徹夜明けかな？\w6\nおやすみ。"
	}
	elseif _timeslot == "昼" || _timeslot == "日中"
	{
		//---- 昼/日中に終了
		"じゃ、また。"
		"さよならっ。"
	}
	else
	{
		//---- 夜/深夜に終了
		"おやすみなさーい。"
	}
	--
	"\-\e"
}


//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************

OnWindowStateRestore
{
	"\1\s[10]やれやれ。\0\s[0]\e"
	"\0\s[0]ふー、復帰だよ。\1\s[10]うむ。\e"
}
