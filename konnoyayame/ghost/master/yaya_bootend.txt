//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- 変数の初期化
	username         = "ユーザーさん"
	teachusername    = 0	// ユーザー名覚え用

	//---- Vanishカウントを見て、真の初回か否かを判定する
	if reference[0] == 0
	{
		//---- ほんとうに初めての起動
		"\u\s[10]\0\s[5]初めまして。\w9\nわたしはややめ。紺野ややめだよ。\w8\1言わずと知れたおれがマック朗だ。\w8\0\s[6]\n\nかじったのはわたしじゃないよ。\w8\1\n\n違ったのか‥\w5‥\w5\w8\0\s[5]\n\nそれはともかく。\w9\nわたしは、YAYAのサンプルゴースト。\w9\nつまり、わたしを踏み台に、新しいゴーストさんを作ってもらうために存在しています。\w8\1\s[11]\n\nおれを踏み台にしたぁ！？\w8\0\s[4]\n\n違うってば。\w8\s[1]\nだ、だから、わたしの中を良く見て、いっぱいいじってみてくださいね？\w8\1\s[10]\n\nちょっと待てホントに待て、その言い方はマジで待って。\w8\0\s[5]\n\n冗談はともかく。\w9\nこれから、少しの間か、長いおつきあいになるかはわからないけど、よろしく、%(username)。\w8\1\n\nよろしくな。\e"
	}
	else
	{
		//---- 再インストール後の初回起動
		""
		--
		if reference[0] == 1
		{
			//vanish１回なら誤射かもしれない
			"\0\s[0]また会いましたね。\w5\1\s[10]前回は勘違いで消したのか？\w5\0\s[6]‥\w5‥\w5‥\w5間違いって言い訳は、一回しか通じないからね。\w8\1地味な脅迫をするな。"
		}

		if reference[0] > 1 && reference[0] < 10
		{
			"\0\s[6]‥\w5‥\w5人間、いろんな事柄に慣れるって言うけど。\w8\1消され慣れるってのも切ない話だな。\w8\0\s[7]\n\nわざわざ入れ直すなら、消さないでください！\w8\1\n\n怒るな怒るな。\w8\0\n\nハードディスクが痛むから！\w8\1\s[11]\n\nそっちか！\e"
		}

		if reference[0] >= 100
		{
			//やりすぎの例
			"\0\s[4]‥\w5‥\w5さすがに教えて。\w9\nなにがしたいんですか。\w8\1‥\w5‥\w5まあ、１００回以上も出し入れされることを予想する方もする方だけどな。\w8\0\n\nよろしく、って言う気も起きないよ。\w8\1\n\n‥\w5‥\w5そう言うなって。\e"
		}

		else
		{
			"\0\s[0]あ、%(username)。\w8\1よお。\w8\0\s[4]\n\nさて、今度はいつ消されるんだろうね。\w8\1\n\nていうか慣れすぎだ、おれら。"
			"\0\s[0]\n\n‥\w5‥\w5%(reference[0])回も消しておいて、いまさら勘違いだなんてことはないでしょ。\n/
			%username、飽きっぽいって言われない？\w5"
		}
		--
		"\1\n\n‥\w5‥\w5ま、\w9まあ、いいだろ。\0\n\nそうだね。またよろしく。\e"
	}
}


//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************

OnBoot
{
	//---- まず双方のサーフィスを表示
	"\0\s[0]\1\s[10]"
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot
	
	case _timeslot{
	when "朝"
	{
		"\0おはよう、%usernameっ。\w5\1今日も一日がんばるぞ。\w5\0\n\n何を？\w5\1\n\n%msの尻を撫でる\w5‥\w5‥かな。\e"
		"\1おっす。\w5\0んー、わたし朝に弱くて。%usernameはどう？\e"
		"\0\s[5]お\w5は\w5ヨー♪\w8\1ご機嫌だな。\w8\0\s[4]\n\nぐー。\w8\1\n\n寝るなっての。\e"
	}
	when "昼"
	{
		"\0\s[5]こんにちは、%(username)。\w8\1ちょうど良いところに来たな。%(food)でも食うか？\e"
		"\0こんにちは、%username。お昼はもう食べたのかな？\w5\1なあ、俺らも%foodでも食おうよ。\w5"
		--
		"\0\n\n別のにしようよ\w5‥\w5‥\e"
		"\0\n\nそうだね、行こうか。\e"
	}
	when "日中"
	{
		"\0こんにちは、%username。\w6\n\nお仕事は順調？勉強ははかどってるかな？\e"
		"\0\s[5]こんにちは、%(username)。\w8\1なんだ、息抜きか。\w8\0\s[6]\n\nぷ〜〜〜。\w8\1\n\n本当に息を抜くな。\e"
	}
	when "夜"
	{
		"\0こんばんは、%username。\w6\1なあ、%mhって何時閉店だっけ？\w5\0\n\nもう無理だよ。/
			\1\n\nふわああ、%WW2officerサイン入り%goods買いそびれたあ。\0\w5\n\n‥\w5‥\e"
		"\0\s[1]こ、こんばんは‥\w5‥\w5\w8\1何を慌ててるんだよ。\w8\0\n\nシャワー浴びてたから、慌てて来たんだよ？\w8\1\n\n服なんか着なくても、別にどってこと無いだろ。\w8\0\s[7]\n\nぷー！\e"
	}
	others
	{
		//それ以外の時間の場合
		"\0まーよーなーかーだー。\w5\1\s[10]腹減った。%food食いてえにょ。\w5\0\n\n太るよ\w5‥\w5‥。\e"
	}
	}//caseの終わり
}

//------------------------------------------------------------------------------
//朝/昼/夜などをおおざっぱに調べる関数
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
	when 22,23,0,1,2,3 {'深夜'}
	when 4,5,6,7,8,9,10,11 {'朝'}
	when 12,13 {'昼'}
	when 14,15,16,17 {'日中'}
	when 18,19,20,21 {'夜'}
	}//caseの終わり
}


//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
	"\1\s[10]ん、終わりか。\w5\0\s[0]"
	--
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に終了
		"徹夜明けかな？\w6\nおやすみ。"
		"\0\s[4]徹夜？徹夜なんだね？\w8\1決めつけるな。\w8\0\s[1]\n\n夜通し何をしてたのかな？\w8\1\n\nうるさいよ。\w8\0\s[5]\n\nばいばい、%(username)。\w9\n目の下のクマに襲われないようにね。\w8\1\n\n‥\w5‥\w5だーかーら。\e"
		"\0\s[1]お出かけ前に、会いに来てくれたのかな‥\w5‥\w5\w8\1むしろお前は、いつ呼ばれたのか覚えてるのか。\w8\0\s[5]\n\nいつだっけ。\w8\1\n\n‥\w5‥\w5だめだこりゃ。\w8\0\n\nまたね、%(username)。"
	}
	elseif _timeslot == "昼" || _timeslot == "日中"
	{
		//---- 昼/日中に終了
		"\0\s[2]え、この時間におでかけ？\w8\1いつ出かけたっていいじゃないか。\w8\0\s[4]\n\nつまんなーいのー。\w8\1\n\n‥\w5‥\w5またな。\e"
		"\0\s[5]よっ、重役出勤。\w8\1‥\w5‥\w5どこで覚えた、そんな言葉。\w8\0\n\nいってらっしゃい、またね。\e"
	}
	elseif _timeslot == "夜"
	{
		"\0\s[5]うん、早寝早起きは健康の秘訣だよね。\w8\1早寝遅起きは男のロマンだ。\w8\0\s[4]\n\n‥\w5‥\w5ちいさいなぁ。\w8\s[5]\nまたね、%(username)。\e"
		"\0\s[4]わたしも寝るー。\w9\n%(username)と寝るー。\w8\1無理だ諦めろ。\w8\0\n\nぷー。\e"
	}

	else
	{
		//---- 夜/深夜に終了
		"\0\s[7]夜更かしー！\w8\1落ち着け。\w8\0\s[5]\n\nふかしふかしあるところに、おじいさんとおばあさんが住んでいました。\w8\1\n\n‥\w5‥\w5なんだそりゃ。\w8\0\n\n日本ふかし話。\w8\1\n\n‥\w5‥\w5%(username)、またな。\w8\0\s[4]\n\nぷー。\e"
		"\0\s[4]あー、やっと寝られる‥\w5‥\w5\w8\1そういう事を言うんじゃないよ。\w8\0\s[3]\n\nおやすみね、%(username)。\w9\n明日もがんばろ。\w8\1\n\nおやすみ。\e"
	}
	--
	"\-\e"
}


//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************

OnWindowStateRestore
{
	"\1\s[10]やれやれ。\0\s[0]\e"
	"\0\s[0]ふー、復帰だよ。\1\s[10]うむ。\e"
}
