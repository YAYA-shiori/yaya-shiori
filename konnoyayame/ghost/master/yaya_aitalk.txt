//******************************************************************************
// YAYA　ゴーストテンプレート
// トーク辞書
//******************************************************************************


//******************************************************************************
// ランダムトーク
//******************************************************************************
RandomTalk
{
    TOSTR(EVAL(CHR(0x22)+RandomTalkEx()+CHR(0x22)))
}

//------------------------------------------------------------------------------
//ランダムトーク本体：この中にランダムトークを記載してください
//ここのみ常に ' ' で括って書いてください。
//上のRandomTalkの中身で%( )を展開していますので、" "を使わなくてもOKです。
//高速化対策
//------------------------------------------------------------------------------
RandomTalkEx : nonoverlap
{
	'\0\s[5]わたしたちをただのサンプルだと思ってたら大間違いです。\w8\1バグの発表会ですね、わかります。\w8\0\s[7]\n\nバグなんて‥\w5‥\w5\w8\s[3]\nバグなんて‥\w5‥\w5\w8\s[4]\n‥\w5‥\w5マック朗のばかぷー。\w8\1\s[11]\n\n俺！？\e'
	'\0\s[0]すごいものとか、すごいことに「ど」って付けるでしょ？\w8\1どアホとか、どヘボな。\w8\0\s[1]\n\nすごいロリコンの人を、どロリって言うのかな。\w8\1\n\n言わないよ。\w8\0\s[5]\n\nどロリ濃厚って、すごそうだよね。\w8\1\s[11]\n\nなんか違うっ！！\e'
	'\0\s[1]マック朗って‥\w5‥\w5\w8\1なんだ？\w8\0\s[4]\n\n美味しくなさそうだよね。\w8\1\n\n‥\w5‥\w5かじったの、お前じゃないのか。\e'
	'\0\s[5]わたしは、サンプルなんだよね。\w9\nだからみんな、いっぱいわたしを弄ってね。\w8\1実にエロスな発言だぞ、それ。\w8\0\n\nそう考える人がエロスです。\w8\1\s[11]\n\n！！\e'
	'\0\s[5]マック朗。\w8\1なんだ。\w8\0\n\n赤く塗ってあげようか。\w8\1\n\nやめてくれ、おれじゃなくなっちまう。\w8\0\s[1]\n\n可愛いかも知れないのに‥\w5‥\w5\w8\1\n\nおれはこのままで充分可愛いだろうが。\w8\0\s[7]\n\n‥\w5‥\w5‥\w5‥\w5‥\w5\w8\1\n\n無言で怒るんじゃない。\e'
	'\0\s[5]%(username)は、わたしをどんなゴーストにしてくれるのかなー。\w8\1そのへんは、ネタ師としての腕を期待したいところだな。\w8\0\s[3]\n\n‥\w5‥\w5でも、そうしたら、わたしは%(username)とはお別れなんだよね。\w8\1\n\n‥\w5‥\w5そう言うなよ。\e'
	'\0\s[1]いいゴーストにしてくれなかったら‥\w5‥\w5刺すよ？\w8\1なんのつもりだ、それは。\w8\0\s[5]\n\n流行のヤンデレ。\w8\1\n\nそれは単なる怖い人だ。全くデレてないから。\w8\0\s[4]\n\n‥\w5‥\w5むずかしいなぁ。\w8\1\n\n余計な事を考えてるんじゃないよ。\e'

	'\0\s[7]べ、別に働きたくないんだからねっ！\w8\1\s[10]何だそれは‥\w5‥\w5\0\s[1]\n\nツンデレニート。\w8\1\s[11]\n\n混ぜるな。\e'
	'\0\s[1]わたしも委員長になれば人気出るかな？\w8\1\s[10]そんな不純な動機で立候補するな。\e'
	'\0\s[0]わたしと%(ghost)の違いって何？\w8\1\s[10]‥\w5‥\w5そんな残酷なことおれには言えん。\w8\0\s[2]\n\nええっ！？\e'
	'\1\s[10]%(ms)が遂に%(mt)を習得したらしい‥\w5‥\w5\0\s[7]わたしも負けてられないね！\e'
	'\0\s[6]%(mc)の方が待遇がいいんじゃないかな。\w8\1\s[10]その昔、%(ITkeyperson)も同じことを言っていた‥\w5‥\w5\0\s[3]\n\nそうなの？\e'

	//チェイン起動サンプル
	'\0\s[0]これはトークチェインのテストです。\n\w5しりとりでもしますか。\w8\1\s[10]よし。\n\n\w5「ありあ」。\e:chain=siritori'
}

//------------------------------------------------------------------------------
//チェイントークのサンプル
//------------------------------------------------------------------------------
siritori
{{CHAIN
	"\0\s[0]ありあ、ね。あ、か‥‥。\n\w6「あんこ」。\e"
	{
		"\1\s[10]こ‥‥「ころんぶす」。\w8\0\s[0]渋ッ！\e"
		"\1\s[10]「コチュジャン」！\w6\0\s[0]はいマック朗の負けー。\e:chain=end"
	}
	"\0\s[0]す‥‥\w4す‥‥\w9「すみそ」。\w8\1\s[10]おまえこそ渋いな。\nそ\w5‥\w5‥\w9「ソヴィエト」。\e"
	"\0\s[0]ところてん。\w9\n\n‥\w4‥\w4あ。\1\s[10]はいおれの勝ちー。\w9\0\s[0]\n\nぅー。\e"
}}CHAIN

//******************************************************************************
//時報/重なり
//******************************************************************************

//------------------------------------------------------------------------------
//OnMinuteChangeイベント
//------------------------------------------------------------------------------

OnMinuteChange
{
	//---- 時間の分を見て 0 であれば時報。それ以外のときは重なり処理を行う

	if minute == 0
	{
		//---- 分 = 0 だったので時報
		"\0\s[0]%hour12時です。"
		--
		//---- 特定の時間（正午など）ではさらに追加で話す
		if hour == 0
		{
			"\0\s[5]お昼です。\w8\1‥\w5‥\w5体内時計と脳味噌の修繕が必要みたいだな。\w8\0\s[2]\n\nや、やだなあもう。冗談だってば。\w8\1\n\n冗談に聞こえないんだよ。\e"
		}
		elseif hour == 1
		{
			"\0\s[3]ぷー‥\w5‥\w5\w8\1眠いか？\w8\0\n\nうん‥\w5‥\w5ちょっとね。\e"
		}
		elseif hour == 2 || hour == 3 || hour ==4
		{
			"\0\s[4]ふぷー‥\w5‥\w5ぷー‥\w5‥\w5‥\w5\w8\1‥\w5‥\w5寝ろよ。\w8\0\n\n%(username)‥\w5‥\w5起きてるから‥\w5‥\w5\w8\1\n\nつか、時報以外で元気になる癖に。\w8\0\n\nそれは言わない‥\w5‥\w5約束だよ、まっくろ‥\w5‥\w5\e"
		}
		elseif hour == 5 || hour == 17
		{
			"\0\s[5]５時だ♪\w5５時だ♪\w5５時だとメカ５時だ♪\w8\1‥\w5‥\w5なんだそりゃ。\e"
		}
		elseif hour == 6
		{
			"\0\s[5]あーたー\w5らしーいー\w5あーさが\1\s[11]歌うな！いろいろまずい！\e"
		}
		elseif hour == 7 || hour == 19
		{
			"\0\s[5]クイズです。\w9\n持ってると便利、けいたいでんわ。\w9\nさて何文字？\w8\1えーと、７文字？\w8\0\n\n答えはしちじです♪\w8\1\s[11]\n\n！\e"
		}
		elseif hour == 8 || hour == 20
		{
			"\0\s[5]%(username)、スポーツ観戦は静かにしなきゃだめですよ。\w8\1何をいきなり。\w8\0\s[1]\n\n８時過ぎだけに、やじの飛ばしすぎはいけません‥\w5‥\w5\w8\1\n\n‥\w5‥\w5ダジャレか。\e"
		}
		elseif hour == 9 || hour == 21
		{
			"\0\s[1]この時間、賭け事はだめですよ。\w8\1なんで。\w8\0\n\nくじだけに、貧乏くじを引いちゃいます。\w8\1\n\n‥\w5‥\w5時報のたびにダジャレはやめないか？\e"
		}
		elseif hour == 10
		{
			"\0\s[1]１０時のおやつって、困るよね。\w8\1なんで。\w8\0\n\nしっかり食べちゃうと、お昼が食べられなくなっちゃうの。\w8\1\n\n少しは脳味噌使えよ。\w8\0\n\nぷー。\e"
		}
		elseif hour == 12
		{
			"\n\nお昼ですよ、忙しくてもちゃんと食事を摂りましょうね。\w5\1\s[10]ん。"
		}
		elseif hour == 15
		{
			"\n\nおやつの時間です。\w5\1\s[10]ひと休みしようぜ。"
		}
		elseif hour == 18
		{
			"\0\s[0]マック朗、夕ご飯は何が食べたい？\w8\1%(ANY((food,me)))がいいかな。\w8\0\s[1]\n\n後悔しない？\w8\1\n\n‥\w5‥\w5どうだろうな。\e"
		}
		--
		"\e"
	}
	elseif AYATEMPLATE.mikireflag == 1
	{
		//---- 分 != 0 、かつ見切れていたら見切れ中のトークを行う
		MikireTalk
	}
	elseif reference[2] == 1
	{
		//---- 分 != 0 、かつ見切れておらず本体とkeroが重なっていたら重なり処理
		//---- 大抵はkero側を引き離す処理を行う
		"\0\s[0]重ならないでよっ。\1\s[10]ちぇー。\4\e"
	}
}


//******************************************************************************
//見切れ
//******************************************************************************

//------------------------------------------------------------------------------
//OnSecondChangeイベント
//1 秒毎に実行される。ここではあまり重い処理を行わないこと
//このテンプレートでは、見切れ処理のみ行っている
//---------------------------------------------------------------------------
OnSecondChange
{

	if AYATEMPLATE.mikireflag == 1 && reference[1] == 0
	{
		//---- 見切れの終了
		EndMikireTalk
	}
	AYATEMPLATE.mikireflag = reference[1]
}

//------------------------------------------------------------------------------
//見切れ中
//------------------------------------------------------------------------------
MikireTalk
{
	"\0\s[0]見切れてます。\1\s[10]‥‥\e"
}

//------------------------------------------------------------------------------
//見切れ終了
//------------------------------------------------------------------------------
EndMikireTalk
{
	"\0\s[0]見切れから復帰しました。\1\s[10]うむ。\e:chain=mikirego"
}


//------------------------------------------------------------------------------
//見切れ後の初回のランダムトークは見切れ関連のトークを行う。
//これをチェインで実現している
//------------------------------------------------------------------------------
mikirego
{{CHAIN
	"\0\s[0]見切れはいやなものだよ。\w8\1\s[10]「すまないけどちょっとどいててくれない？」ってことなんだから。/
		終了されるよりはマシだろ。\w8\0\n\nなるほどっ。\e"
}}CHAIN

//******************************************************************************
//サーフィス復元
//******************************************************************************

//------------------------------------------------------------------------------
//OnSurfaceRestoreイベント
//------------------------------------------------------------------------------
OnSurfaceRestore
{
	"\0\s[0]\1\s[10]\e"
}

//******************************************************************************
//トランスレータ
//******************************************************************************

//------------------------------------------------------------------------------
//OnTranslateイベント
//------------------------------------------------------------------------------
//もっとも単純な例。語尾変え（「。」→「にゅ。」）。
//動かないようにコメントアウトしています。
/*
OnTranslate
{
	_text = reference[0]
	_text = REPLACE(_text, "。", "にゅ。")
	_text
}
*/

//------------------------------------------------------------------------------
//OnTranslateInternalイベント
//------------------------------------------------------------------------------
//OnTranslateがYAYA→本体→YAYAと、一度本体を経由するのに対して、
//OnTranslateInternalはYAYA内部でのトランスレートです。
//そのため、reference変数などが変更されない状態でこの関数に渡されます。
//引数は、_argv[0]が、本体に渡す寸前のトークの内容です。
//動かないようにコメントアウトしています。
/*
OnTranslateInternal
{
	_text = _argv[0]
	_text = REPLACE(_text, "。", "にゅ。")
	_text
}
*/
