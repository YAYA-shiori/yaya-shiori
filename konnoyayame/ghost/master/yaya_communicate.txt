//******************************************************************************
// YAYA　ゴーストテンプレート
// コミュニケート辞書
//******************************************************************************


//******************************************************************************
//こちらから話しかける
//******************************************************************************

//この関数は現状メニューから呼ばれます
StartCommunicate
{
	//以下の関数は、隣に立っているゴーストを探して、
	//TalkTo_??? 関数を呼び出すものです。誰も居なければランダムトークをします。
	//yaya_tmpl_utilにあります。
	AYATEMPLATE.ExecuteTalkToTalk
}

//エミリに話しかける
TalkTo_Emily{
	"\0\s[0]あ、エミリさん。\1\s[10]テディも元気そうだな。"
}


//その他の人に話しかける
TalkToOther{
	"\0\s[0]こんにちは、%(res_reference0)さん。\1\s[10]元気でやってるか。"
}

TalkToNobody{
	"\0\s[0]だれもいないよ。\1\s[10]静かなもんだ。"
}


//******************************************************************************
//話しかけられたので返事する
//******************************************************************************

//------------------------------------------------------------------------------
//OnCommunicateイベント
//------------------------------------------------------------------------------
OnCommunicate
{
	if reference[0] == "user" || reference[0] == "User"
	{
		//---- ユーザーからの話しかけ
		ReplyToUser
	}
	else
	{
		//---- ゴーストからの話しかけ
		//以下の関数は、ReplyTo_??? 関数を呼び出すものです。
		//yaya_tmpl_utilにあります。
		AYATEMPLATE.ExecuteReplyToTalk(reference[0])
	}
}

//------------------------------------------------------------------------------
//ユーザーからの話しかけ
//------------------------------------------------------------------------------
ReplyToUser
{
	if "名前" _in_ reference[1] && "覚えて" _in_ reference[1]
	{
		//---- ユーザー名入力開始
		"\0\s[0]いいよ、なんていう名前なの、%(username)？\e"
		teachusername = 1
	}
	else
	{
		//---- ユーザー名入力開始以外
		if teachusername == 1
		{
			//---- 直前が「名前覚えて」だったので名前を覚える
			username = reference[1]
			"\0\s[0]%(username)、ね。わかりました。\e"
			teachusername = 0
		}
		else
		{
			//---- 名前覚え以外のさまざまな会話への返事
			TalkToUser
		}
	}
}

//------------------------------------------------------------------------------
//ユーザーへの返事（名前覚え以外）
//------------------------------------------------------------------------------
TalkToUser
{
	if "こんにちは" _in_ reference[1]
	{
		"\0\s[0]こんにちはっ。\e"
	}
	elseif "好き" _in_ reference[1] || "愛し" _in_ reference[1]
	{
		"\0\s[0]ありがとうございます。\nわたしも%(username)のこと‥‥\w9、好きかも\w5‥\w5‥。\e"
	}
	elseif "ややめ" _in_ reference[1] && "好物" _in_ reference[1]
	{
		"\0\s[0]わたしの好きなたべものはねー、レーズンがいーっぱい入ったバターパンなの。/
			\w5\1\s[10]おれは%(food)がいーっぱい入った餃子だな。\e"
	}
	else
	{
		"\0\s[0]なんて言いました？\e"
	}
}

//------------------------------------------------------------------------------
//ゴーストからの話しかけ
//------------------------------------------------------------------------------

//エミリから話しかけられた
ReplyTo_Emily{
	res_reference0=reference[0]	//さらに相手に話しかける
	"\0\s[0]なんですか、エミリさん。\1\s[10]芋ばっかり食べてちゃだめだぞ。\e"
}

ReplyTo_Hand{
	if "ほっく" _in_ reference[1] {
		res_reference0=reference[0]	//さらに相手に話しかける
		"\0\s[0]ほっくなんて無いよ。\1\s[10]ジッパーはあるけどな。\e"
	}else{
		"\1\s[10]うるさい手だな。\0\s[0]なんだろうねあれ。\e"
	}
}


//その他の人から話しかけられた
ReplyToOther{
	if "こんにちは" _in_ reference[1] || "こんばんは" _in_ reference[1]
	{
		res_reference0=reference[0]	//さらに相手に話しかける
		"\0\s[0]どうも、%(res_reference0)。\1\s[10]うむ。%(reference[1])\e"
	}
	else
	{
		"\0\s[0]はい？\e"
	}
}

