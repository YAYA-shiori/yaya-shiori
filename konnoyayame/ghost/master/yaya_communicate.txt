///////// YAYA　ゴーストテンプレート
///////// コミュニケート辞書


//**** こちらから話しかける *************************************************************

StartCommunicate
{

	//---- 話しかける
	if res_reference[0] == "なる"
	{
		//---- 奈留へ話しかけ
		"\0\s[0]元気ですか、奈留さん。\e"
	}
	elseif res_reference[0] == "さくら"
	{
		//---- さくらへ話しかけ
		"\0\s[0]どもー。\e"
	}
	elseif res_reference[0] != ""
	{
		//---- 誰かへ話しかけ
		"\0\s[0]こんにちは、%res_reference[0]。\e"
	}
	else
	{
		//---- 誰もいない　この場合はランダムトークを行う
		RandomTalk
	}
}


//**** 話しかけられたので返事する *******************************************************

//---- OnCommunicateイベント ------------------------------------------------------------
OnCommunicate
{
	if reference[0] == "user" || reference[0] == "User"
	{
		//---- ユーザーからの話しかけ
		ReplyToUser
	}
	else
	{
		//---- ゴーストからの話しかけ
		ReplyToGhost
	}
}

//---- ユーザーからの話しかけ -----------------------------------------------------------
ReplyToUser
{
	if "名前" _in_ reference[1] && "覚えて" _in_ reference[1]
	{
		//---- ユーザー名入力開始
		"\0\s[0]いいよ、なんていう名前なの、%username？\e"
		teachusername = 1
	}
	else
	{
		//---- ユーザー名入力開始以外
		if teachusername == 1
		{
			//---- 直前が「名前覚えて」だったので名前を覚える
			username = reference[1]
			"\0\s[0]%username、ね。わかりました。\e"
			teachusername = 0
		}
		else
		{
			//---- 名前覚え以外のさまざまな会話への返事
			TalkToUser
		}
	}
}

//---- ユーザーへの返事（名前覚え以外） -------------------------------------------------
TalkToUser
{
	if "こんにちは" _in_ reference[1]
	{
		"\0\s0こんにちはっ。\e"
	}
	elseif "好き" _in_ reference[1] || "愛し" _in_ reference[1]
	{
		"\0\s0ありがとうございます。\nわたしも%usernameのこと‥‥\w9、好きかも\w5‥\w5‥。\e"
	}
	elseif "ややめ" _in_ reference[1] && "好物" _in_ reference[1]
	{
		"\0\s0わたしの好きなたべものはねー、レーズンがいーっぱい入ったバターパンなの。/
			\w5\1\s[10]おれは%foodがいーっぱい入った餃子だな。\e"
	}
	else
	{
		"\0\s0なんて言いました？\e"
	}
}

//---- ゴーストからの話しかけ -----------------------------------------------------------
ReplyToGhost
{
	// 話しかけてきた相手に返事
	res_reference[0] = reference[0]

	if res_reference[0] == "なる"
	{
		//---- 奈留へ返事
		res_reference[0] = ""
		"\0\s0はい。\e"
	}
	elseif res_reference[0] == "さくら"
	{
		//---- さくらへ返事
		if "うるさい" _in_ reference[1]
		{
			res_reference[0] = ""
			"\0\s0‥‥ぅー。\e"
		}
		elseif "こんにちは" _in_ reference[1]
		{
			"\0\s0こんちはー。\e"
		}
		elseif "また今度" _in_ reference[1]
		{
			res_reference[0] = ""
			"\1\s[10]んむ。\e"
		}
		else
		{
			"\0\s0はい、そうですね。\e"
		}
	}
	else
	{
		//---- 誰かへ返事
		if "こんにちは" _in_ reference[1] || "こんばんは" _in_ reference[1]
		{
			"\0\s0どうも、%res_reference[0]。\e"
		}
		else
		{
			res_reference[0] = ""
			"\0\s0はァ。\e"
		}
	}
}
