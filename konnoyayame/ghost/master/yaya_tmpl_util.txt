///////// YAYA　ゴーストテンプレート
///////// メニュー処理辞書

//**** 注意 *************************************************************
// この辞書はゴーストテンプレートに必要な共通処理をまとめたものです。
// 通常はここをいじる必要はありません。
//**** 注意 *************************************************************


//**** 起動・終了初期化 *******************************************************************
OnGhostLoad.AYATEMPLATE
{
	AYATEMPLATE.stroke = IARRAY
	AYATEMPLATE.strokeid = IARRAY
	AYATEMPLATE.stroketimer = IARRAY
	AYATEMPLATE.mikireflag = 0
	AYATEMPLATE.LastTalk = ''
}

OnGhostUnload.AYATEMPLATE
{
	ERASEVAR('AYATEMPLATE.stroke')
	ERASEVAR('AYATEMPLATE.strokeid')
	ERASEVAR('AYATEMPLATE.stroketimer')
	ERASEVAR('AYATEMPLATE.mikireflag')
	ERASEVAR('AYATEMPLATE.LastTalk')
}

//**** ランダムトーク *******************************************************************

OnAITalk
{
	OnAiTalk
}

//---- OnAiTalkイベント -----------------------------------------------------------------
OnAiTalk
{
	//---- 通常のランダムトーク、ただしチェイン中はチェイントーク
	if CHAIN.IDName == "" {
		AYATEMPLATE.LastTalk = RandomTalk
	}
	else {
		AYATEMPLATE.LastTalk = ChainTalk
	}
	AYATEMPLATE.LastTalk
}

//**** メニュー関係 *************************************************************

//---- イベント処理 ---------------------------------------------------------------
//ここの記述により、選択肢ID/アンカーIDをそのまま関数名として書けるようになります。
//以下のサンプルも参照して下さい。

OnChoiceSelect
{
	if ISFUNC(reference[0]) {
		EVAL(reference[0])
	}
}

OnAnchorSelect
{
	if SUBSTR(reference[0],0,7) == 'http://' {
		if basewarename == 'SSP' {
			"\C"
		}
		--
		"\j[%(AYATEMPLATE.EscapeText(reference[0]))]"
	}
	else {
		if ISFUNC(reference[0]) {
			EVAL(reference[0])
		}
	}
}

//---- メニュー横1行反転 ---------------------------------------------------------------
//通常メニュー
AYATEMPLATE.MenuItem
{
	_linefeed = 100
	_n = _argc
	if _n >= 3 {
		if GETTYPE(_argv[_n-1]) == 1 { //整数
			_linefeed = _argv[_n-1]
			_n -= 1
		}
	}

	_lftext = ''
	while _linefeed > 0 {
		if _linefeed >= 100 {
			_lftext += '\n'
		}
		else {
			_lftext += '\n[half]'
		}
		_linefeed -= 100
	}
	
	_lftext
	--
	'\![*]\q['
	--
	AYATEMPLATE.EscapeText( AYATEMPLATE.MakeJustText(_argv[0],46) )
	--
	_s = ''
	for _i = 1 ; _i < _n ; _i++ {
		_s += ',' + AYATEMPLATE.EscapeText(_argv[_i])
	}
	_s
	--
	']'
}

//エスケープ
AYATEMPLATE.EscapeText
{
	_r = _argv[0]
	if RE_SEARCH(_r,'["\[\]]') >= 0 {
		'"' + REPLACE(_r,'"','""') + '"'
	}
	else {
		_r
	}
}

//---- 後ろにスペースを補完する関数
AYATEMPLATE.MakeLongText
{
	_menuitem = _argv[0]
	_len = _argv[1] - GETSTRBYTES(_menuitem,127)

	if _len <= 0 {
		_menuitem
		return
	}
	
	_space = '　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　'
	_menuitem += SUBSTR(_space,0,_len / 2)
	
	if _len % 2 > 0 {
		_menuitem += " "
	}
	_menuitem
}

//---- 適当な長さで切る -------------------------------------------
AYATEMPLATE.MakeShortText
{
	if _argc < 2 {
		'';
		return;
	}
	
	_num = _argv[1] / 2
	_len = STRLEN(_argv[0])
	_text = ''
	
	if _num >= _len {
		_argv[0]
		return;
	}
	
	_lendiff = 0
	while 1 {
		_text = SUBSTR(_argv[0],0,_num)
		
		_lendiff = GETSTRBYTES(_text,127) - _argv[1] + 1
		if _lendiff >= 0 {
			break;
		}
		_num += 1
		if _num >= _len {
			break;
		}
	}
	
	if _num >= _len {
		_argv[0]
	}
	else {
		_text = SUBSTR(_argv[0],0,_num - 1)
		if _lendiff > 0 {
			_text += '..'
		}
		else {
			_text += '...'
		}
		_text;
	}
}

//---- 長さをきっちり調節
AYATEMPLATE.MakeJustText
{
	_text = AYATEMPLATE.MakeLongText(_argv[0],_argv[1])
	if _text != _argv[0] {
		_text
		return
	}
	
	_text = AYATEMPLATE.MakeShortText(_argv[0],_argv[1])
	_text
}

//**** リンクメニュー *************************************************************

AYATEMPLATE.LinkMenuConvert
{
    _text = ''
    _data = IARRAY
    for _i = 0 ; _i < _argc ; _i++ {
        _data = (RE_SPLIT(_argv[_i],'[ \t]*\|[ \t]*'),'','') //ダミー
        _text += "%(_data[0])%(CHR(1))%(_data[1])%(CHR(1))%(_data[2])%(CHR(1))%(CHR(2))"
    }
    _text;
}

//**** システムで使われる文字置換 *************************************************************

AYATEMPLATE.TranslateSystemChar
{
  _text = TOSTR(_argv[0])
  _text = RE_REPLACE(_text,"[ !%(CHR(0x22))%(CHR(0x25))#$&()*+,\-/:;<=>?@\[\]`{|}~]","_")
  _text
}

//**** 切り替えコア *************************************************************

AYATEMPLATE.ExecuteChangeCallTalk
{
	_ghostname = AYATEMPLATE.TranslateSystemChar(reference[0])

	_funcname = "%(_argv[0])_%(_ghostname)"
	if ISFUNC(_funcname) {
		_script = EVAL(_funcname)
		if STRLEN(_script) != 0 {
			_script
			return
		}
	}

	_funcname = "%(_argv[0])Other"
		if ISFUNC(_funcname) {
		_script = EVAL(_funcname)
		if STRLEN(_script) != 0 {
			_script
			return
		}
	}

	if _argc >= 2 {
		_script = EVAL(_argv[1])
		if STRLEN(_script) != 0 {
			REPLACE(_script,"\-","")
			return
		}
	}
}

//**** マウスイベントコア *************************************************************

AYATEMPLATE.MouseEventExec
{
	_fname = _argv[0] + TOSTR(reference[3]) + TOSTR(reference[4])
	if ISFUNC(_fname) {
		EVAL(_fname)
		return
	}
	
	_fname = _argv[0] + TOSTR(reference[3])
	if ISFUNC(_fname) {
		EVAL(_fname)
		return
	}
}

AYATEMPLATE.MouseMoveWheelFunc
{
	_side = TOINT(reference[3])
	if _argv[0] {
		_side += 10
	}
	
	if AYATEMPLATE.strokeid[_side] == reference[4] {
		if (GETTICKCOUNT - TOINT(AYATEMPLATE.stroketimer[_side])) > 1500 {
			AYATEMPLATE.stroke[_side] = 1
		}
		AYATEMPLATE.stroketimer[_side] = GETTICKCOUNT
		
		_stroke = TOINT(AYATEMPLATE.stroke[_side])
		_stroke += 1
		AYATEMPLATE.stroke[_side] = _stroke
		
		if _stroke >= _argv[1] {
			AYATEMPLATE.MouseEventExec(_argv[2]);
			AYATEMPLATE.stroke[_side] = 1
		}
	}
	else {
		AYATEMPLATE.stroke[_side] = 1
		AYATEMPLATE.stroketimer[_side] = GETTICKCOUNT
		AYATEMPLATE.strokeid[_side] = reference[4]
	}
}

//**** 文字列関係 *************************************************************

//---- SHIORI 関連情報 ------------------------------------------------------------------
On_version
{
	GETSETTING('coreinfo.version')
}

On_craftman
{
	GETSETTING('coreinfo.author')
}

On_craftmanw
{
	GETSETTING('coreinfo.author')
}

On_name
{
	GETSETTING('coreinfo.name')
}



